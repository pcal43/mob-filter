plugins {
	// https://plugins.gradle.org/plugin/net.neoforged.moddev
	id 'net.neoforged.moddev' version '2.0.116'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven {
		name = 'NeoForge'
		url = 'https://maven.neoforged.net/releases'
	}
}

neoForge {
    version = project.neoforge_version
    runs {
        client {
            type = "client"
            gameDirectory = file("${project.projectDir}/run")
        }
        server {
            type = "server"
            gameDirectory = file("${project.projectDir}/run")
        }
    }
    mods {
        mobfilter_forge {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
	implementation project(":common")
	//additionalRuntimeClasspath project(":common") // FIXME? this is needed for runs to work, don't understand why
	testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_version}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junit_version}"
}

test {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/neoforge.mods.toml") {
		filter { line ->
			line.replace('${version}', project.version.toString())
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

